plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
    id("org.jetbrains.kotlin.plugin.compose")
    id("org.jetbrains.kotlin.kapt")
    alias(libs.plugins.hilt)
    alias(libs.plugins.detekt)
    alias(libs.plugins.ktlint)
}

configure<org.jlleitschuh.gradle.ktlint.KtlintExtension> {
    filter {
        exclude("**/*.kts")
    }
}

android {
    namespace = "io.signallq.pro"
    compileSdk = libs.versions.compileSdk.get().toInt()

    defaultConfig {
        applicationId = "io.signallq.pro"
        minSdk = libs.versions.minSdk.get().toInt()
        targetSdk = libs.versions.targetSdk.get().toInt()
        // Contador PROPRIO do Pro (libs.versions.toml: proVersionCode/proVersionName) —
        // nunca reusar o contador do consumidor (":app"). Ver nota no catalogo de versoes.
        versionCode = libs.versions.proVersionCode.get().toInt()
        versionName = libs.versions.proVersionName.get()

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            // MVP0 roda local/emulador/APK debug — sem Firebase/Play do Pro ainda
            // (conta nova pendente de aprovacao do Luiz, ver issue #1158).
        }
        release {
            isMinifyEnabled = true
            isShrinkResources = true
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro",
            )
            // Sem signingConfig ainda — release do Pro fora de escopo do MVP0 (sem
            // Play Console/keystore proprios definidos, issue #1158).
        }
    }

    buildFeatures {
        compose = true
        buildConfig = true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        encoding = "UTF-8"
    }

    testOptions {
        unitTests {
            isIncludeAndroidResources = true
        }
    }
}

kotlin {
    compilerOptions {
        jvmTarget.set(org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_17)
    }
}

kapt {
    correctErrorTypes = true
}

detekt {
    buildUponDefaultConfig = true
    config.setFrom("$rootDir/config/detekt.yml")
    baseline = file("$rootDir/config/detekt-baseline.xml")
}

ktlint {
    version = "1.3.1"
    android = true
    ignoreFailures = false
    reporters {
        reporter(org.jlleitschuh.gradle.ktlint.reporter.ReporterType.PLAIN)
    }
}

dependencies {
    // Fase 2 (issue #1161) -- feature modules do Grupo 1 (trimmed) e Grupo 2 do prototipo.
    // :pro:app so compoe os grafos de navegacao de cada feature em ProNavHost -- nenhuma
    // regra de negocio aqui.
    implementation(project(":pro:core:designsystem"))
    implementation(project(":pro:core:database"))
    implementation(project(":pro:feature:auth"))
    implementation(project(":pro:feature:cliente"))
    implementation(project(":pro:feature:visita"))
    implementation(project(":pro:feature:ambiente"))
    implementation(project(":pro:feature:medicao-diagnostico"))
    // Compat de DI para reaproveitar :featureSpeedtest (ver ProSpeedtestCompatModule.kt) --
    // infra generica reutilizavel, zero acoplamento ao consumidor.
    implementation(project(":coreNetwork"))
    implementation(project(":coreTelephony"))
    implementation(project(":coreDatastore"))
    implementation(project(":featureSpeedtest"))

    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.activity.ktx)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(platform(libs.compose.bom))
    implementation(libs.compose.ui)
    implementation(libs.compose.ui.tooling.preview)
    implementation(libs.compose.material3)
    implementation(libs.activity.compose)
    implementation(libs.lifecycle.runtime.compose)
    implementation(libs.androidx.navigation.compose)
    implementation(libs.hilt.navigation.compose)
    debugImplementation(libs.compose.ui.tooling)

    implementation(libs.hilt.android)
    kapt(libs.hilt.compiler)
    implementation(libs.timber)

    testImplementation(libs.junit)
    testImplementation(libs.kotlinx.coroutines.test)
    testImplementation(platform(libs.compose.bom))
    testImplementation(libs.compose.ui.test.junit4)
    debugImplementation(libs.compose.ui.test.manifest)
    androidTestImplementation(libs.androidx.junit)
    androidTestImplementation(libs.androidx.espresso.core)
}
